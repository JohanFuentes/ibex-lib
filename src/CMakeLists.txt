
################################################################################
# libibex
################################################################################
set (IBEX_SOURCES "")
set (IBEX_HEADERS "")
set (IBEX_INCDIRS "")

# Add files from interval library wrapper
list (APPEND IBEX_SOURCES ${INTERVAL_LIB_WRAPPER_SRC})
list (APPEND IBEX_HEADERS ${INTERVAL_LIB_WRAPPER_DIR}/ibex_IntervalLibWrapper.h
                        ${INTERVAL_LIB_WRAPPER_DIR}/ibex_IntervalLibWrapper.inl)
list (APPEND IBEX_INCDIRS ${INTERVAL_LIB_WRAPPER_DIR})

# Generate ibex_Setting.h and add its path to the list of include directories
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/ibex_Setting.h.in
  ${CMAKE_CURRENT_BINARY_DIR}/ibex_Setting.h
)
list (APPEND IBEX_INCDIRS ${CMAKE_CURRENT_BINARY_DIR})

# Recurse on all subdirectories
set (SUBDIR_LIST arithmetic bisector combinatorial contractor data function
                 numeric parser predicate set strategy symbolic system tools)

foreach (subdir ${SUBDIR_LIST})
  unset (IBEX_SUBDIR_SOURCES)
  unset (IBEX_SUBDIR_HEADERS)
  unset (IBEX_SUBDIR_INCDIRS)
  add_subdirectory (${subdir})
  list (APPEND IBEX_SOURCES ${IBEX_SUBDIR_SOURCES})
  list (APPEND IBEX_HEADERS ${IBEX_SUBDIR_HEADERS})
  list (APPEND IBEX_INCDIRS ${IBEX_SUBDIR_INCDIRS})
endforeach()

# Configure flex and bison for the parser
find_package(BISON)
find_package(FLEX)
FLEX_TARGET(Lexer
  ${CMAKE_CURRENT_SOURCE_DIR}/parser/lexer.l
  ${CMAKE_CURRENT_BINARY_DIR}/parser/lexer.lex.cc
  COMPILE_FLAGS "-Pibex")
list (APPEND IBEX_SOURCES ${FLEX_Lexer_OUTPUTS})
BISON_TARGET(Parser
  ${CMAKE_CURRENT_SOURCE_DIR}/parser/parser.yc
  ${CMAKE_CURRENT_BINARY_DIR}/parser/parser.tab.cc
  COMPILE_FLAGS "--name-prefix=ibex --report=all --file-prefix=parser")
list (APPEND IBEX_SOURCES ${BISON_Parser_OUTPUTS})

ADD_FLEX_BISON_DEPENDENCY (Lexer Parser)
list (APPEND IBEX_INCDIRS ${CMAKE_CURRENT_BINARY_DIR}/parser)

# Generate header ibex.h
set (IBEX_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/ibex.h)
file (WRITE ${IBEX_MAIN_HEADER} "/* This file in generated by CMake */\n\n")
file (APPEND ${IBEX_MAIN_HEADER} "#ifndef __IBEX_H__\n#define __IBEX_H__\n\n")
file (APPEND ${IBEX_MAIN_HEADER} "#include \"ibex_Setting.h\"\n")
foreach (header_path ${IBEX_HEADERS})
  GET_FILENAME_COMPONENT (header_name ${header_path} NAME)
  file (APPEND ${IBEX_MAIN_HEADER} "#include \"${header_name}\"\n")
endforeach()
file (APPEND ${IBEX_MAIN_HEADER} "\n#endif /* __IBEX_H__ */\n")

# Create the target for libibex
add_library (ibex STATIC ${IBEX_SOURCES} ${IBEX_HEADERS})
target_include_directories(ibex PUBLIC ${IBEX_INCDIRS})
target_include_directories(ibex PUBLIC ${INTERVAL_LIB_INCLUDE_DIRS})
target_compile_options (ibex PUBLIC ${INTERVAL_LIB_CXXFLAGS})
target_link_libraries (ibex PUBLIC ${INTERVAL_LIB_LIBRARIES})

# Add files that should be installed
install (TARGETS ibex DESTINATION lib)
install (FILES ${IBEX_MAIN_HEADER} DESTINATION include)
install (FILES ${IBEX_HEADERS} DESTINATION include/ibex)
